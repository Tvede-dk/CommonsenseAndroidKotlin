class MultiDirRes implements Plugin<Project> {

    @Override
    void apply(final Project project) {
        //is it an android project ? if so then apply the plugin.
        if (project.pluginManager.hasPlugin("com.android.application")
                || project.pluginManager.hasPlugin("com.android.library")) {
            applyMultiSourceDir(project)
        } else {
            println("Not android based project, thus disabling plugin.")
        }
    }

    final def resDirs = [
            "anim",
            "color",
            "drawable",
            "mipmap",
            "drawable-hdpi",
            "drawable-mdpi",
            "drawable-xhdpi",
            "drawable-xxhdpi",
            "layout",
            "values",
    ]

    void applyMultiSourceDir(Project project) {
        project.afterEvaluate {
            println "Multi res dir enabled for $project.name"
            project.android.sourceSets.each {
                def rootResDir = it.res.srcDirs[0]
                List<File> srcDirs = resDirs.collect {
                    getSubDirs(it, rootResDir)
                }.flatten()
                it.res.srcDirs = [rootResDir] + srcDirs
            }
        }

    }
    final def getSubDirs = { final dirName, final rootResDir ->
        final def resDir = new File(rootResDir, dirName)
         def subLayoutDirs = []
        if (resDir.exists()) {

            resDir.eachDir {
                subLayoutDirs.add it
                println "\t - adding dir $it to category $dirName"
            }
        }
        return subLayoutDirs
    }
}

apply plugin: MultiDirRes

